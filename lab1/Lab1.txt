:-set_prolog_flag(answer_write_options,[max_depth(0)]).


%Параметры сферической опоры (тип, диаметр резьбы, код)

support(sphere,16,'3.910.02').

support(sphere,12,'3.910.01').

support(sphere,8,'2.910.02').

support(sphere,6,'2.910.01').


%Параметры опоры с насечкой (тип, диаметр резьбы, код)

support(knurl,12,'3.911.01').

support(knurl,8,'2.911.02').

support(knurl,6,'2.911.01').


%Параметры штыря (тип, диаметр резьбы, код, минимальный диаметр, максимальный диаметр)

support(pin,12,'3.213.06',12,26).

support(pin,8,'2.213.04',8,12).

support(pin,6,'2.213.01',6,8).


%Параметры кулачка с резьбовым отверстием (тип, типоразмер, высота кулачка, диаметр резьбы, код)

jaw(hole,'65_30',38,12,'2.913.09').

jaw(hole,'45_22',20,8,'2.913.06').

jaw(hole,'30_18',16,6,'2.913.05').


%Параметры кулачка с призматическими пазами (тип, типоразмер, высота кулачка, Dг min, Dг max, Dв min, Dв max, код)

jaw(prizmatic,'65_30',25,12,30,8,18,'2.913.07').

jaw(prizmatic,'45_22',12,8,12,3,7,'2.913.02').

jaw(prizmatic,'30_18',10,8,12,3,7,'2.913.01').


%Параметры зажима (типоразмер зажима, типоразмер кулачка, высота зажима, код)

clamp('90_60','65_30',42,'3.451.02').

clamp('60_45','45_22',35,'3.451.01').

clamp('60_45','45_22',34,'2.451.02').

clamp('45_30','30_18',29,'2.451.01').


%Параметры прокладки (типоразмер, высота прокладки, код)

gasket('90_60',5,'3.107.28').

gasket('90_60',3,'3.107.27').

gasket('90_60',2,'3.107.25').


gasket('60_45',5,'3.217.10').

gasket('60_45',3,'3.217.09').

gasket('60_45',2,'3.217.07').

gasket('60_45',1,'3.217.01').


gasket('45_30',5,'2.217.10').

gasket('45_30',3,'2.217.09').

gasket('45_30',2,'2.217.07').

gasket('45_30',1,'2.217.01').



%Предикат формирования пакета прокладок (Типоразмер, количество, высота блока, список кодов)

gaskets(_,0,H,[]):-H=0,!.

gaskets(_,0,H,[]):-H<0,!,fail.

gaskets(SizeType,N,H,[Type|T]):-

	gasket(SizeType,H1,Type),

	H2 is H-H1,

	gaskets(SizeType,N1,H2,T),

	N is N1+1,

	!.

%Предикаты формирования подвижной части (тип поверхности, диаметр, код кулачка, 
%код опоры, высота кулачка, типоразмер кулачка)


%Формирование подвижной части для плоской чистовой заготовки

movepart(finplane,_,JawCode,SupportCode,HJaw,JawSizeType):-
	
	support(sphere,DM,SupportCode), %Выбираем опору

	jaw(hole,JawSizeType,HJaw,DM,JawCode).%Выбираем кулачок



%Формирование подвижной части для плоской черновой заготовки

movepart(roughplane,_,JawCode,SupportCode,HJaw,JawSizeType):-
	
	support(knurl,DM,SupportCode), %Выбираем опору

	jaw(hole,JawSizeType,HJaw,DM,JawCode). %Выбираем кулачок



%Формирование подвижной части для перфорированной заготовки

movepart(perforated,D,JawCode,SupportCode,HJaw,JawSizeType):-
	
	nonvar(D),
	support(pin,DM,SupportCode,Dmin,Dmax), %Выбираем опору

	D>=Dmin, %Проверяем, что диаметр опоры соответствует диаметру заготовки

	D=<Dmax,
	jaw(hole,JawSizeType,HJaw,DM,JawCode). %Выбираем кулачок



%Формирование подвижной части для цилиндрической вертикальной заготовки

movepart(cylindrvert,D,JawCode,_,HJaw,JawSizeType):-

	nonvar(D),
	jaw(prizmatic,JawSizeType,HJaw,_,_,DVmin,DVmax,JawCode), %Выбираем кулачок

	D>=DVmin, %Проверяем, что кулачок подходит для заготовки.
	
	D=<DVmax.



%Формирование подвижной части для цилиндрической горизонтальной заготовки

movepart(cylindrhor,D,JawCode,_,HJaw,JawSizeType):-
	
nonvar(D),
jaw(prizmatic,JawSizeType,HJaw,DHmin,DHmax,_,_,JawCode), %Выбираем кулачок

	D>=DHmin, %Проверяем, что кулачок подходит для заготовки.

	D=<DHmax.



%Предикаты построения УСП(универсально-сборного приспособления)

%Параметры (Высота точки зажима, вид поверхности заготовки, диаметр, количество прокладок, коды прокладок, код зажима,
%код кулачка, код опоры)

machinetool(H,BlankType,D,GasketAmount,GasketCodes,ClampCode,JawCode,SupportCode,OutList):-

	movepart(BlankType,D,JawCode,SupportCode,HJaw,JawSizeType), %Определяем подвижную часть
	
	clamp(ClampSizeType,JawSizeType,HClamp,ClampCode), %Выбираем зажим

	HGaskets is H-HJaw-HClamp-30, %Определяем высоту прокладок

	gaskets(ClampSizeType,GasketAmount,HGaskets,GasketCodes), %Подбираем прокладки под необходимую высоту

	%nl,

	printgaskets(OutList,GasketCodes).

	%nl.
countallsolutions(C,H,BlankType,D):-

	findall(GA,machinetool(H,BlankType,D,GA,_,_,_,_,_),List),

	length(List,C).


uniqlist([],[]):-!.

uniqlist([H|T],Out):-
	member(H,T),uniqlist(T,Out).

uniqlist([H|T],[H|T1]):-
	not(member(H,T)),uniqlist(T,T1).


printgaskets(OutList,GasketsList):-

	uniqlist(GasketsList,UniqueGaskets),

	addgroups(List,GasketsList,UniqueGaskets),

	printdatatolist(OutList,List,UniqueGaskets),

	!.


printdatatolist([],[],[]).

printdatatolist([[H1:H2]|T],[H1|T1],[H2|T2]):-

	printdatatolist(T,T1,T2).


addgasketstogroup(0,[],_).

addgasketstogroup(C,[H|T],H1):-
	
	H1\=H,
	addgasketstogroup(C,T,H1).

addgasketstogroup(C,[H|T],H1):-
	H1=H,

	addgasketstogroup(C1,T,H1),

	C is C1+1.

addgroups([],_,[]).

addgroups([C1|T],GasketsList,[H1|T1]):-
	
	addgasketstogroup(C1,GasketsList,H1),

	addgroups(T,GasketsList,T1). 


